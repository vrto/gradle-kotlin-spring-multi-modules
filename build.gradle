buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}"
        classpath "org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "io.spring.gradle:dependency-management-plugin:${springDependencyManagementVersion}"
    }
}

// old syntax for plugins needed so their versions can be referenced from 'gradle.properties`
apply plugin: 'java'
apply plugin: 'org.jetbrains.kotlin.jvm'
apply plugin: 'org.jetbrains.kotlin.plugin.spring'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group 'sk.vrto'
version '1.0-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "http://repo.spring.io/libs-snapshot" }
}

dependencies {
    // internal modules
    compile project(':shared')
    compile project(':customers')
    compile project(':users')
    testCompile project(path: ':shared', configuration: 'testRuntime')

    // external libraries
    compile 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    compile 'org.jetbrains.kotlin:kotlin-reflect'
    compile 'org.springframework.boot:spring-boot-starter-web'

    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile 'junit:junit:4.12'
    testCompile 'io.rest-assured:rest-assured:3.2.0'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

jar {
    baseName 'multi-modules'
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'org.jetbrains.kotlin.plugin.allopen'
    apply plugin: 'org.jetbrains.kotlin.plugin.spring'
    apply plugin: 'io.spring.dependency-management'

    repositories {
        maven {
            url "http://repo.spring.io/libs-snapshot"
        }
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
        compile 'org.jetbrains.kotlin:kotlin-reflect'
        compile 'org.springframework.boot:spring-boot-starter-web'

        testCompile 'org.springframework.boot:spring-boot-starter-test'
        testCompile "junit:junit:${junitVersion}"
        testCompile "io.rest-assured:rest-assured:${restAssuredVersion}"
    }

    // spring boot plugin may only be applied to the root project
    dependencyManagement {
        imports { mavenBom("org.springframework.boot:spring-boot-dependencies:${springBootVersion}") }
    }
}

// shared module can't depend on self, but all the other projects need it
configure(subprojects.findAll { it.name != 'shared' }) {
    dependencies {
        compile project(':shared')
        testCompile project(path: ':shared', configuration: 'testRuntime')
    }
}